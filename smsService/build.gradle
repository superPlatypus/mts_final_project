plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.4'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'org.openapi.generator' version '6.0.1'
}

group = 'ru.mts'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '11'
}


repositories {
    mavenCentral()
}
dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2021.0.4"
    }
}
dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    // https://mvnrepository.com/artifact/com.twilio.sdk/twilio
    implementation 'com.twilio.sdk:twilio:10.2.1'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.11'
    implementation 'org.springdoc:springdoc-openapi-data-rest:1.6.11'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.1'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/src/main/resources/sms-api.yaml"
    outputDir = "$buildDir/generated"
    apiPackage = "ru.mts.api"
    modelPackage = "ru.mts.model"
    configOptions = [
            dateLibrary: "java8"
    ]
}

sourceSets {
    main {
        java {
            srcDirs += ["$buildDir/generated/src/main/java", "src/main/java"]
        }
    }
}

compileJava {
    dependsOn tasks.openApiGenerate
}

tasks.named('test') {
    useJUnitPlatform()
}
